#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba16_snorm) uniform image2D lighting_buffer;
layout(set = 0, binding = 1, r16ui) uniform uimage2D depth_buffer;
layout(set = 0, binding = 2, r8ui) uniform uimage2D normal_buffer;
layout(set = 0, binding = 3, rgba16_snorm) uniform writeonly image2D final_output;

layout(push_constant) uniform PushData {
    int size;
} push_data;

ivec2 sampleAt(ivec2 offset) {
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy) + offset;
    if (pixel.x < 0) pixel.x = 0;
    if (pixel.y < 0) pixel.y = 0;
    if (pixel.x >= imageSize(lighting_buffer).x) pixel.x = imageSize(lighting_buffer).x - 1;
    if (pixel.y >= imageSize(lighting_buffer).y) pixel.y = imageSize(lighting_buffer).y - 1;
    return pixel;
}

#define SAMPLE(DX, DY, WEIGHT) \
{ \
    ivec2 pos = sampleAt(ivec2(DX, DY) * push_data.size); \
    float dist = imageLoad(depth_buffer, pos).r / 256.0; \
    float distance_difference = 4.0 * abs(center_distance - dist); \
    uint normal = imageLoad(normal_buffer, pos).r; \
    float normal_difference = normal == center_normal ? 0 : 10; \
    float weight = WEIGHT / (distance_difference + normal_difference + 1.0); \
    total_weight += weight; \
    sum += imageLoad(lighting_buffer, pos).rgb * weight; \
}

void main() {
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    float center_distance = imageLoad(depth_buffer, pixel).r / 256.0;
    uint center_normal = imageLoad(normal_buffer, pixel).r;

    if (center_normal < 16) {
        float total_weight = 0.146634;
        vec3 sum = imageLoad(lighting_buffer, pixel).rgb * total_weight;
        SAMPLE( 0,  1, 0.092566);
        SAMPLE( 0, -1, 0.092566);
        SAMPLE( 1,  0, 0.092566);
        SAMPLE(-1,  0, 0.092566);

        SAMPLE( 1,  1, 0.058434);
        SAMPLE(-1,  1, 0.058434);
        SAMPLE(-1, -1, 0.058434);
        SAMPLE( 1, -1, 0.058434);

        SAMPLE( 2,  0, 0.023205);
        SAMPLE(-2,  0, 0.023205);
        SAMPLE( 0,  2, 0.023205);
        SAMPLE( 0, -2, 0.023205);

        SAMPLE( 2,  2, 0.003672);
        SAMPLE(-2,  2, 0.003672);
        SAMPLE(-2, -2, 0.003672);
        SAMPLE( 2, -2, 0.003672);

        SAMPLE( 2,  1, 0.014648);
        SAMPLE(-2,  1, 0.014648);
        SAMPLE(-2, -1, 0.014648);
        SAMPLE( 2, -1, 0.014648);
        SAMPLE( 1,  2, 0.014648);
        SAMPLE(-1,  2, 0.014648);
        SAMPLE(-1, -2, 0.014648);
        SAMPLE( 1, -2, 0.014648);

        SAMPLE( 3,  0, 0.002289);
        SAMPLE(-3,  0, 0.002289);
        SAMPLE( 0,  3, 0.002289);
        SAMPLE( 0, -3, 0.002289);

        SAMPLE( 3,  1, 0.001445);
        SAMPLE(-3,  1, 0.001445);
        SAMPLE(-3, -1, 0.001445);
        SAMPLE( 3, -1, 0.001445);
        SAMPLE( 1,  3, 0.001445);
        SAMPLE(-1,  3, 0.001445);
        SAMPLE(-1, -3, 0.001445);
        SAMPLE( 1, -3, 0.001445);
        imageStore(final_output, pixel, vec4(sum / total_weight, 1.0));
    } else {
        imageStore(final_output, pixel, imageLoad(lighting_buffer, pixel));
    }

}